Imports Microsoft.CodeAnalysis
Imports Microsoft.CodeAnalysis.Text
Imports Microsoft.CodeAnalysis.VisualBasic.Syntax
Imports IGIC = Microsoft.CodeAnalysis.IncrementalGeneratorInitializationContext

<Generator(LanguageNames.VisualBasic, LanguageNames.CSharp)>
Public NotInheritable Class EventRaiserGen
    Implements IIncrementalGenerator

    Private Class EventInfo
        Public Property EventName As String
        Public Property ModuleName As String
        Public Property EventType As String
        Public Property Parameters As List(Of ParameterInfo)
        Public Property Location As Location
    End Class

    Private Class ParameterInfo
        Public Property Name As String
        Public Property Type As String
    End Class

    Public Sub Initialize(context As IGIC) Implements IIncrementalGenerator.Initialize
        ' Create a pipeline for VB.NET syntax nodes
        Dim syntaxProvider = context.SyntaxProvider.CreateSyntaxProvider(
            Function(syntaxNode, token)
                ' Check if the node is an EventStatementSyntax (VB.NET event declaration)
                Dim eventDecl = TryCast(syntaxNode, EventStatementSyntax)
                If eventDecl Is Nothing Then Return False

                ' Check if the event is declared inside a module by looking at ancestors
                Dim moduleDecl = eventDecl.FirstAncestorOrSelf(Of ModuleBlockSyntax)()
                If moduleDecl Is Nothing Then Return False

                Return True
            End Function,
            Function(gsc, token)
                Dim eventDecl = DirectCast(gsc.Node, EventStatementSyntax)
                
                ' Find the containing module using FirstAncestorOrSelf
                Dim moduleBlock = eventDecl.FirstAncestorOrSelf(Of ModuleBlockSyntax)()
                Dim moduleStatement = moduleBlock?.BlockStatement
                
                ' Return nothing if not in a module (should be filtered out by predicate)
                If moduleStatement Is Nothing Then Return Nothing

                Return New EventInfo With {
                    .EventName = eventDecl.Identifier.ValueText,
                    .ModuleName = moduleStatement.Identifier.ValueText,
                    .EventType = If(eventDecl.AsClause IsNot Nothing,
                        eventDecl.AsClause.Type.ToString(), "EventHandler"),
                    .Parameters = GetEventParameters(eventDecl),
                    .Location = moduleStatement.GetLocation()
                }
            End Function
        )

        ' Filter out any null values from the provider
        Dim filteredEvents = syntaxProvider.Where(Function(e) e IsNot Nothing)

        ' Group events by module name
        Dim groupedByModule = filteredEvents.Collect().
            Select(Function(events, token) events.GroupBy(Function(e) e.ModuleName))

        ' Register the source output
        context.RegisterSourceOutput(groupedByModule,
            Sub(sourceContext, moduleGroups)
                For Each modGroup As IGrouping(Of String, EventInfo) In moduleGroups
                    Dim moduleName = modGroup.Key
                    Dim eventsInModule = modGroup.ToList()

                    ' Generate a single file for this module with all event raisers
                    Dim sourceCode = GenerateModuleRaiseMethods(moduleName, eventsInModule)
                    Dim fileName = $"{moduleName}_EventRaisers.g.vb"

                    sourceContext.AddSource(
                        fileName, SourceText.From(sourceCode, System.Text.Encoding.UTF8))
                Next modGroup
            End Sub)
    End Sub

    Private Shared Function GetEventParameters(eventDecl As EventStatementSyntax) As List(Of ParameterInfo)
        Dim parameters As New List(Of ParameterInfo)

        If eventDecl.ParameterList IsNot Nothing Then
            For Each paramSyntax In eventDecl.ParameterList.Parameters
                ' Handle parameter with or without AsClause
                Dim paramType = If(paramSyntax.AsClause IsNot Nothing,
                                   paramSyntax.AsClause.Type.ToString(),
                                   "Object") ' Default to Object if no type specified

                parameters.Add(New ParameterInfo With {
                    .Name = paramSyntax.Identifier.Identifier.ValueText,
                    .Type = paramType
                })
            Next paramSyntax
        End If

        Return parameters
    End Function

    Private Shared Function GenerateModuleRaiseMethods(moduleName As String, events As List(Of EventInfo)) As String
        Dim code As New System.Text.StringBuilder

        ' Add file header
        code.AppendLine("' <auto-generated>")
        code.AppendLine("'     This code was generated by `ModuleEventRaiser.Generator`.")
        code.AppendLine("'     Changes to this file may cause incorrect behavior and will be lost if")
        code.AppendLine("'     the code is regenerated.")
        code.AppendLine("' </auto-generated>")
        code.AppendLine()
        code.AppendLine("Option Explicit On")
        code.AppendLine("Option Strict On")
        code.AppendLine()
#Region "ToDo: Add other required imports when necessary"
        code.AppendLine("Imports System")
#End Region
        code.AppendLine()

        ' Begin module
        code.AppendLine($"Partial Public Module {moduleName}")
        code.AppendLine()

        ' Generate raise methods for each event in this module
        For Each evtInfo As EventInfo In events
            ' Skip if event name is empty
            If String.IsNullOrWhiteSpace(evtInfo.EventName) Then Continue For

            ' Build parameter list for the raise method
            Dim paramList As New List(Of String)
            For Each pInfo As ParameterInfo In evtInfo.Parameters
                paramList.Add($"{pInfo.Name} As {pInfo.Type}")
            Next pInfo
            Dim params = String.Join(", ", paramList)

            ' Build argument list for RaiseEvent
            Dim argList As New List(Of String)
            For Each pInfo As ParameterInfo In evtInfo.Parameters
                argList.Add(pInfo.Name)
            Next pInfo
            Dim args = String.Join(", ", argList)

            ' Generate the raise method
            code.AppendLine($"    ''' <summary>")
            code.AppendLine($"    ''' Raises the {evtInfo.EventName} event.")
            code.AppendLine($"    ''' </summary>")

            ' Add parameter documentation
            For Each pInfo As ParameterInfo In evtInfo.Parameters
                code.AppendLine($"    ''' <param name=""{pInfo.Name}"">The {pInfo.Name} parameter.</param>")
            Next pInfo

            code.AppendLine($"    Public Sub RaiseEvent_{evtInfo.EventName}({params})")
            code.AppendLine($"        RaiseEvent {evtInfo.EventName}({args})")
            code.AppendLine($"    End Sub")
            code.AppendLine()
        Next evtInfo

        ' End module
        code.AppendLine("End Module")

        Return code.ToString()
    End Function
End Class